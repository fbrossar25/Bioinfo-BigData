buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
        h2DBVersion = '1.4.197'
        hibernateVersion = '5.3.7.Final'
        junitJupiterVersion = '5.3.1'
        jacksonVersion = '2.9.7'
        defaultEncoding = 'UTF-8'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'application'

group 'fr.unistra'
version '1.0-SNAPSHOT'
sourceCompatibility = 1.8
mainClassName = 'fr.fbrossar25.jsg.Main'
compileJava.options.encoding = "${defaultEncoding}"
compileTestJava.options.encoding = "${defaultEncoding}"
applicationDefaultJvmArgs = ["-Dfile.encoding=${defaultEncoding}"]

repositories {
    mavenCentral()
}

dependencies {
    implementation("org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}")
    implementation("com.h2database:h2:${h2DBVersion}")
    testImplementation("org.springframework.boot:spring-boot-starter-test:${springBootVersion}")
    testImplementation("org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}")
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '3.17'
    implementation group: 'org.apache.poi', name: 'poi', version: '3.17'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    implementation("com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}")
    implementation("com.fasterxml.jackson.core:jackson-core:${jacksonVersion}")
    implementation("com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}")
    implementation group: 'org.hibernate', name: 'hibernate-core', version: "${hibernateVersion}"

    implementation group: 'com.google.guava', name: 'guava', version: '26.0-jre'
    implementation "com.github.rholder:guava-retrying:2.0.0"
    implementation("org.biojava:biojava-core:5.1.1"){
        exclude group: 'org.apache.logging.log4j'
    }

}

//println "${rootProject.projectDir}"

clean {
    delete("${rootProject.projectDir}/db", "${rootProject.projectDir}/logs")
}

jar {
    manifest {
        attributes("Main-Class": mainClassName)
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

distributions {
    main {
        contents {
            into ('lib') {
                from jar
                from(project.configurations.runtime)
            }
        }
    }
}
